buildscript{
    ext {
        branch = System.getenv('BRANCH_NAME') != null ? System.getenv('BRANCH_NAME') :
                ( System.getenv('USER')!=null ? System.getenv('USER') : System.getenv('USERNAME') )
        dependencyRepo = ( branch == "release" ? "libs-release" : "libs-snapshot" )
        deployVersion = ( branch == "release" ? project.version : "${project.version}-SNAPSHOT" )
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
    repositories {
        maven { url "$artifactoryUrl/$dependencyRepo"}
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

ext.beeswaxVersion = "2017-03-16"

mainClassName = 'com.beeswax.hexbid.server.HexbidServer'

repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile 'io.netty:netty-all:4.1.7.Final'
    compile 'com.google.guava:guava:21.0'
    compile 'org.apache.logging.log4j:log4j-api:2.7'
    compile 'org.apache.logging.log4j:log4j-core:2.7'
    compile "com.beeswax:beeswax-api:${beeswaxVersion}"
    compile 'org.mockito:mockito-core:2.6.2'
    compile 'org.apache.maven.plugins:maven-javadoc-plugin:2.10.4'
    testCompile 'junit:junit:4.11'
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

//jar {
//    manifest {
//        attributes (
//                'Implementation-Title': (project.group + "." + project.name),
//                'Implementation-Version': version,
//                'Implementation-Timestamp': new Date(),
//                "Implementation-Vendor": mftVendor,
//                "Specification-Title": mftTitle,
//                "Specification-Version": version,
//                "Specification-Vendor": mftVendor,
//                "Bundle-License": pomLicense,
//                'Class-Path': configurations.runtime.files.collect { "../lib/$it.name" }.join(' ') + " ../conf/",
//                'Main-Class': project.mainClassName
//        )
//    }
//}

//below configuration will overwrite normal .jar artifact by following same naming standards
//created artifact will be shadow-jar (a.k.a. uber-jar, or fat-jar) for self-execution
shadowJar {
    baseName = project.name
    classifier = 'all'
    version = deployVersion
}

//task sourcesJar(type: Jar) {
//    classifier 'sources'
//    from sourceSets.main.allJava
//}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom.withXml {
                def root = asNode()

                root.appendNode("name", mftTitle)
                root.appendNode("description", mftTitle)
                root.appendNode("url", project.scmUrl)
                def organization = new Node(root, "organization")
                organization.appendNode("name", mftVendor)
                organization.appendNode("url", mftVendorUrl)
                def license = new Node(new Node(root, "licenses"), "license")
                license.appendNode('name', pomLicense)
                license.appendNode('distribution', 'repo')
                def scm = new Node(root, "scm")
                scm.appendNode("connection", scmConnection)
                scm.appendNode("developerConnection", scmDevConnection)
                scm.appendNode("url", scmUrl)
                def dependencies = root.get("dependencies")
                root.remove(dependencies)
                root.append(dependencies)
            }
        }
    }
}

artifacts {
    archives shadowJar
}

distTar.enabled = false
distZip.enabled = false

//build.dependsOn('clean', 'sourcesJar')
build.dependsOn('clean')
